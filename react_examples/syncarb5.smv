--syncarb.smv:

MODULE arbiter-element(above,below,init-token)

VAR
  Persistent : boolean;
  Token : boolean;
  Request : boolean;

ASSIGN
  init(Token) := init-token;
  next(Token) := token-in;
  init(Persistent) := FALSE;
  next(Persistent) := Request & (Persistent | Token);

DEFINE
  above.token-in := Token;
  override-out := above.override-out | (Persistent & Token);
  grant-out := !Request & below.grant-out;
  ack-out := Request & (Persistent & Token | below.grant-out);

MODULE main

VAR
  e5 : arbiter-element(self,e4,FALSE);
  e4 : arbiter-element(e5,e3,FALSE);
  e3 : arbiter-element(e4,e2,FALSE);
  e2 : arbiter-element(e3,e1,FALSE);
  e1 : arbiter-element(e2,self,TRUE);

DEFINE
  grant-in := TRUE;
  e1.token-in := token-in;
  override-out := FALSE;
  grant-out := grant-in & !e1.override-out;



LTLSPEC
    G F e5.Request -> G F (!e5.Request | e5.ack-out)

LTLSPEC
    (G F !e5.Request -> G F e1.ack-out) & 
     (G F !e5.Request -> G F e2.ack-out) & 
      (G F !e5.Request -> G F e3.ack-out) & 
        (G F !e5.Request -> G F e4.ack-out)
